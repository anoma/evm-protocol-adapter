module ERC20Resource;

import Stdlib.Prelude open;
import Applib open;

import Types open;
import Helpers open;

{- LOGIC -}

erc20TokenLogic (pub : Instance) (priv : Witness) : Bool :=
  let
    tag := Instance.tag pub;
  in if
    {- ERC20 resource consumed -}
    | isNullifier tag :=
      case findResourceByNullifier tag priv of {
        | nothing := false
        | just self :=
          if
            {- initializing consumption -}
            | Resource.ephemeral self :=
              requireWrapperContractTransition self priv
            {- conventional consumption -}
            | else :=
              isAuthorizedBy@{
                signer := owner self;
                origin := tag;
                pub;
              }
      }
    {- ERC20 resource created -}
    | isCommitment tag :=
      case findResourceByCommitment tag priv of {
        | nothing := false
        | just self :=
          if
            {- finalizing creation -}
            | Resource.ephemeral self :=
              requireWrapperContractTransition self priv
            {- conventional creation -}
            | else := true
      }
    | else := false;

requireWrapperContractTransition (self : Resource) (priv : Witness) : Bool :=
  -- require wrapper contract resource to be consumed/created
  case Witness.consumed priv, Witness.created priv of
    | [a; b], [x; y] :=
      let
        ((consumedERC20, createdERC20), consumedWC, createdWC) :=
          matchKinds self a b x y;
      in same-kind?
        [
          decodeWrapperResourceKindFromLabel consumedERC20;
          decodeWrapperResourceKindFromLabel createdERC20;
          kind consumedWC;
          kind createdWC;
        ]
    {- The following is redundant since the wrapper contract checks this already.
    && same-kind?
    [
    decodeWrappedResourceKindFromValue consumedWC;
    decodeWrappedResourceKindFromValue createdWC;
    kind self;
    kind consumedERC20;
    kind createdERC20;
    ]
    -}
    | _, _ := false;

{- RESOURCE -}

-- should only be initializable/finalizable if wc is consumed
mkErc20Resource
  (logic : Logic)
  -- (erc20Token : ExternalIdentity)
  (wrapperResourceKind : Kind)
  (owner : ExternalIdentity)
  (quantity : Nat)
  (nonce : Nat)
  {nkc : NullifierKeyCommitment := Universal.nullifierKeyCommitment}
  {ephemeral : Bool := false}
  : Resource :=
  mkResource@{
    logic;
    label := builtinAnomaEncode wrapperResourceKind;
    -- TODO remove -- label := builtinAnomaEncode (wrapperResourceKind, erc20Token);
    value := builtinAnomaEncode owner;
    quantity;
    ephemeral;
    nullifierKeyCommitment := builtinAnomaEncode nkc;
    nonce;
    randSeed := 0;
  };

{- HELPERS -}

decodeWrapperResourceKindFromLabel (wrappedResource : Resource) : Kind :=
  let
    wrapperResourceKind := builtinAnomaDecode (Resource.label wrappedResource);
  -- TODO remove -- (wrapperResourceKind, _) : Pair Kind Address := builtinAnomaDecode (Resource.label wrappedResource);
  in wrapperResourceKind;

owner (resource : Resource) : ExternalIdentity :=
  resource |> Resource.value |> builtinAnomaDecode;
